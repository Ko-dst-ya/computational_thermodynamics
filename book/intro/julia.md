---
substitutions:
  urldownload: "**[[url]](https://julialang.org/downloads/)**"
  wintermdwnld: "<img src=\"https://www.microsoft.com/favicon.ico%3Fv2\" style=\"height: 1em;\" alt=\"⊞\"> [Windows Terminal](https://aka.ms/terminal)"
---

# Основы языка программирования Julia

В данном разделе излагаются основные инструменты языка Julia, которые понадобятся в практикуме.

- Официальная документация языка находится здесь: https://docs.julialang.org/en/v1/.
- Самостоятельно можно обучиться по
  - мануалу https://docs.julialang.org/en/v1/manual/getting-started/;
  - разделу с материалами https://julialang.org/learning/.
- Обзор синтаксиса https://learnxinyminutes.com/docs/ru-ru/julia-ru/.

Здесь и далее prompt-сообщение терминала помечается равняется `%`, например,

```console
% echo $HOME
/Users/stepanzh
```

Поэтому, когда вы встретите `%`, значит команда набиралась в терминале.

## Инструкция по установке

Ниже расположены краткие инструкции по установке исполнителя языка Julia для Linux-based операционных систем (ОС), MacOS и Windows 10. При возникновении неясностей, обратитесь к [официальной инструкции](https://julialang.org/downloads/platform/) вашей ОС.

```{admonition} Инструкция для пользователей Linux-based ОС
:class: dropdown

**1. Скачать последнюю версию интерпретатора**

На сайте загрузок {{ urldownload }} найти в таблице свою систему и архитектуру
(скорее всего, это {guilabel}`Generic Linux on x86 → 64-bit`).

:::{warning}
Использовать пакеты `julia` из репозитория дистрибутива настоятельно не рекомендуется, поскольку в репозиториях сборка часто неверно сконфигурирована, из-за чего сторонние библиотеки для Julia могут отказаться работать.
:::

**2. Распаковать скачанный архив**

(предполагаем, что скачанный архив лежит непосредственно в домашней директории)

:::console
$ tar -xzf julia-1.6.2-linux-x86_64.tar.gz
:::

После этого в домашней директории должна появиться директория {file}`julia-1.6.2`, где находится исполняемый файл интерпретатора и необходимые для его работы библиотеки.

**3. Добавить исполняемый файл в `PATH`**

Рекомендуется вместо добавления в `PATH` директории, в которой лежит исполняемый файл, добавить символьную ссылку на этот файл в директорию, которая уже в `PATH`. В большинстве дистрибутивов директория {file}`~/bin`, если она существует, автоматически добавляется в `PATH`, что позволяет добавить ссылку, даже не имея прав администратора. Если директории {file}`~/bin` нет, то её сначала нужно создать:

:::console
$ mkdir ~/bin
:::

Затем делаем там символьную ссылку на исполняемый файл интерпретатора

:::console
$ ln -s ~/julia-1.6.2/bin/julia ~/bin/julia
:::

Теперь интерпретатор можно запускать, просто набрав `julia` в командной строке (возможно, нужно начать новую сессию в терминале).

Можно вместо домашней директории сделать символьную ссылку в системной директории (нужны права администратора):

:::console
# ln -s ~/julia-1.6.2/bin/julia /usr/local/bin/julia
:::
```

```{admonition} Инструкция для пользователей MacOS
:class: dropdown

1. Скачать `Julia-1.6.app`-пакет с официального сайта языка {{ urldownload }}, {guilabel}`MacOS → 64-bit`;
2. Стандартно для `.app` приложений установить.

Исполнитель языка находится здесь

:::
% /Applications/Julia-1.6.app/Contents/Resources/julia/bin/julia --version
julia version 1.6.2
:::

Однако, чтобы в будущем не доступаться до Julia по абсолютному пути, добавьте `alias` в rc-файл вашего терминала.

::::{admonition} Как найти rc-файл
:class: dropdown

Узнайте ваш `shell`

:::console
% echo $SHELL
/bin/zsh
:::

- Если это `zsh`, то файл для настройки {file}`~/.zshrc`.
    - Если файл отсутствует, создайте его `touch ~/.zshrc`.
- Если это `bash`, то файл для настройки {file}`~/.bashrc`.
    - Если файл отсутствует, создайте его `touch ~/.bashrc`.
::::

Добавление `alias` (для `bash` аналогично):

:::console
% open ~/.zshrc

# в открывшемся редакторе будет содержимое `.zshrc`, добавьте в файл строчку
alias julia='/Applications/Julia-1.6.app/Contents/Resources/julia/bin/julia'
# сохраните .zshrc

% source ~/.zshrc  # изменения вступят в силу (можно также просто перезапустить терминал)
% julia --version  # проверка, всё ли работает
julia version 1.6.2
:::
```

```{admonition} Инструкция для пользователей ОС Windows 10
:class: dropdown

**1. Скачать последнюю версию интерпретатора**

На сайте загрузок {{ urldownload }} найти в таблице свою систему и архитектуру
(скорее всего, подойдет {guilabel}`Windows → 64-bit (installer)`)

**2. Установить интерпретатор**

Следуйте инструкциям установщика. По умолчанию установка идёт в пользовательскую директорию. Если хочется установить в {file}`C:\Program Files`, не забудьте запустить установщик через {guilabel}`ПКМ → Запуск от имени администратора`.

В конце установки отметить галочкой пункт **Add Julia to `PATH`**.

**3. Запуск**

Теперь интерпретатор можно запускать из командной строки командой `julia`.
*Рекомендуется* в качестве командной строки использовать современный {{ wintermdwnld }}.
```

(repl_guide)=
## Интерактивный режим

```{margin}
Это аналогично интерактивному режиму работы в Python.
```
Знакомство языка начнём с интерактивного режима. Для этого необходимо запустить Julia REPL (Read-Evaluate-Print-Loop).

```{margin}
`GNOME Terminal`, `Terminal.app`, `iTerm.app`, `Windows Terminal`, `cmd`...
```
В терминале необходимо набрать команду `julia`

```console
% julia
               _
   _       _ _(_)_     |  Documentation: https://docs.julialang.org
  (_)     | (_) (_)    |
   _ _   _| |_  __ _   |  Type "?" for help, "]?" for Pkg help.
  | | | | | | |/ _` |  |
  | | |_| | | | (_| |  |  Version 1.6.2 (2021-07-14)
 _/ |\__'_|_|_|\__'_|  |  Official https://julialang.org/ release
|__/                   |

julia>
```

Строки до `julia>` это приветственное сообщение. Здесь есть ссылка на документацию к языку. Команды для получения помощи и версия релиза.

Julia REPL обладает четыремя режимами работы. Режим отмечается prompt-строкой.

Строка `julia>` это prompt-сообщение в **исполнительном режиме** REPL-а. В нём вы можете исполнять команды языка и переходить в другие режимы.

```julia-repl
julia> println("Hello, world")
Hello, world

julia>
```

Перейти в **режим помощи** по языку можно с помощью {kbd}`?`, при этом prompt-сообщение сменится на `help?>`.

```{margin}
Попробуйте также зайти в режим помощи и нажать {kbd}`↵ Return`.
```
```julia-repl
help?> println
search: println printstyled print sprint isprint

  println([io::IO], xs...)

  Print (using print) xs followed by a newline. If io is not supplied, prints to stdout.

  ...
```

Чтобы выйти из режима помощи, необходимо нажать {kbd}`← Backspace`. *Таким же способом можно вернуться и из других режимов в **исполнительный***.

```{margin}
Для знакомых с Python, `pkg>` сочетает в себе возможности `pip` и `virtualenv`.
```
Третий режим посвящён работе со **сторонними пакетами и окружениями [^pkgenv]**. Команда перехода {kbd}`]`, prompt-сообщение `(@v1.6) pkg>`, попробуйте набрать `help`. Этот режим нам пока не понадобится.

[^pkgenv]: Программный пакет *package* это библиотека кода. Окружение *environment* позволяет исполнить код только с необходимыми пакетами, что уменьшает количество конфликтов версий между ними.

```{margin}
Бывает удобно, но есть особенности.
```
В четвёртом режиме можно работать в терминале, не выходя из Julia REPL. Команда перехода {kbd}`;`, prompt-сообщение `shell>`.

```julia-repl
shell> echo 'Привет, мир!'
Привет, мир!

shell>
```

## Числа и переменные

В Julia богатая система встроенных числовых типов, узнать тип переменной или литерала можно с помощью функции `typeof(x)`

```julia-repl
julia> typeof(5)
Int64

julia> typeof(5.0)
Float64

julia> typeof(5im)
Complex{Int64}

julia> typeof(2.3 + 5.0im)
ComplexF64 (alias for Complex{Float64})

julia> typeof(2//3)
Rational{Int64}

julia> typeof(1//3 + 2//3im)
Complex{Rational{Int64}}

```

Синтаксис присвоения значения переменной стандартный, также поддерживается параллельное присваивание и каскадное.

```julia-repl
julia> x = 5
5

julia> x, y = 1, 2
(1, 2)

julia> x
1

julia> y
2

julia> x = y = 1
1

julia> x
1

julia> y
1

```

В исполнительном режиме REPL печатает возращаемое значение, его можно опустить с помощью `;` в конце выражения.

```julia-repl
julia> x = 10;

julia> x
10

```

Имена переменных могут быть любыми Unicode символами, REPL позволяет набирать часто употребляемые с помощью LaTeX-подобного синтаксиса и {kbd}`Tab ⇥`, например, так выглядит набор имени `α₁`:

```julia-repl
julia> \alpha  # потом нажмите `<Tab>`, и ввод сменится на α
       α
       α\_1  # `<Tab>`
       α₁
```

Ниже в таблице перечислены некоторы арифметические, алгебраические и тригонометрические операторы и функции

```{list-table}
:header-rows: 1

* - Описание
  - Синтаксис
* - Бинарные `+`, `-`, `*`, `/`
  - `x + y`, `x - y`, `x * y`, `x / y`
* - Умножение на константу ([сахар](https://ru.wikipedia.org/wiki/%D0%A1%D0%B8%D0%BD%D1%82%D0%B0%D0%BA%D1%81%D0%B8%D1%87%D0%B5%D1%81%D0%BA%D0%B8%D0%B9_%D1%81%D0%B0%D1%85%D0%B0%D1%80))
  - `<num>x`, например `2.5x`
* - Унарный минус
  - `-x`
* - Частное от деления целых
  - `x ÷ y` или `div(x, y)`
* - Остаток от деления целых (также см. `divrem`)
  - `x % y` или `rem(x, y)`
* - Возведение в степень
  - `x ^ y`
* - Логарифмы $\ln(x)$, $\log_2(x)$, $\log_{10}(x)$
  - `log(x)`, `log2(x)`, `log10(x)`
* - Тригонометрия $\sin(x)$, $\cos(x)$
  - `sin(x)`, `cos(x)`
* - Квадратный корень $\sqrt{x}$
  - `sqrt(x)` или `√x`
```

Стоит отметить, что (кажется, все) функции из таблицы определены для всех типов, перечисленных выше, но, иногда требуется аргумент подать в явном типе

```julia-repl
julia> sqrt(-1.0)
ERROR: DomainError with -1.0:
sqrt will only return a complex result if called with a complex argument. Try sqrt(Complex(x)).
...

julia> sqrt(-1.0 + 0im)
0.0 + 1.0im

```

```{admonition} Почему?
:class: dropdown

Такое поведение языка задано по двум причинам.

Во-первых, функция корня в математике определяется на разных множествах.

Во-вторых, так удаётся сохранять *стабильность типов*. Если бы функция корня извлекая из `Float64`, возвращала иногда `Float64`, а иногда `Complex{Float64}`, то предсказать тип возвращаемого значения было бы невозможно. Предсказание типов позволяет *компилировать* участки исходного кода, не уступающие по скорости аналогам кода на статически типизируемых языках, например, `C/C++`.
```

Математические константы

```julia-repl
julia> π                    # \pi<Tab>
π = 3.1415926535897...

julia> ℯ                    # \euler<Tab>
ℯ = 2.7182818284590...

julia> typeof(ℯ)
Irrational{:ℯ}

```

При применении операций к разным типам тип результата интуитивен: число из типа, соответствующего, математически "меньшему" множеству конвертируется в тип математически "большего" множества. Затем операция выполняется для одинаковых типов. Система типов в Julia это её позвоночник, см. раздел {ref}`type_system`.

```{margin}
Однако, стоит избегать и потенциально предугадывать места, в которых переменная меняет свой тип. Компилятор вас поощрит.
```
В Julia **динамическая** типизация. Переменная может менять свой тип в процессе работы программы

```julia-repl
julia> x = 5
5

julia> typeof(x)
Int64

julia> x  = 5.0
5.0

julia> typeof(x)
Float64
```

В Julia нельзя указать, чтобы переменная имела постоянное значение, но можно указать, что переменная *не меняет свой тип*

```julia-repl
julia> const y = 10
10

julia> y = 10.0
ERROR: invalid redefinition of constant y
Stacktrace:
 [1] top-level scope
   @ REPL[6]:1

julia> y = 20
WARNING: redefinition of constant y. This may fail, cause incorrect answers, or produce other errors.
20
```

Заметьте, Julia не допустила присвоение `y = 10.0`, потому что оно меняет тип переменной `y::Int64`. Однако, присваивать значения `Int64` переменной `y` можно, Julia лишь *предупредит*, но не запретит.

## Строки

В Julia богатая поддержка работы с текстом, встроенные текстовые типы работают с Unicode.

Тип `Char` используется для работы с отдельными символами, его литерал две одинарные кавычки `'`

% здесь почему-то julia-repl не работает. Убрал, чтобы jb build не ругался
```
julia> 'a'
'a': ASCII/Unicode U+0061 (category Ll: Letter, lowercase)

julia> 'α'
'α': Unicode U+03B1 (category Ll: Letter, lowercase)

julia> 'aa'
ERROR: syntax: character literal contains multiple characters
Stacktrace:
 [1] top-level scope
   @ none:1
```

Тип `String` предназначен для строк, его литерал двойные кавычки `"`

```julia-repl
julia> "a"
"a"

julia> "a" == 'a'
false

julia> "aaaa"
"aaaa"

julia> typeof("aaaa")
String
```

Escape специальных символов производится привычным `\`, например `"\n"`, `"\t"`.

```{list-table} Часто используемые операции над строками.
:header-rows: 1

* - Описание
  - Cинтаксис
  - Пример
  - Результат
* - Конкатенация
  - `x * y`
  - `"ab" * "b"`
  - `"abb"`
* - Дублирование
  - `x ^ i`
  - `"a" ^ 6`
  - `"aaaaaa"`
* - Интерполяция значения переменной
  - `"$x"`
  - `y = 1.0; x = "$y"`
  - `x` будет `"1.0"`
* - Интерполяция результата выражения
  - `"$(expr)"`
  - `"$(sin(π/2))"`
  - `"1.0"`
* - Разбиение строки
  - `split(x)`
  - `split("a b\nc\td")`
  - `["a", "b", "c", "d"]` (массив)
* - Сбор строки
  - `join(x [, delim])`
  - `join([25, "февраля", 1936], " ")`
  - `"25 февраля 1936"`
* - Удаление whitespace окружения
  - `strip(x)`
  - `strip(" \ta ba\n")`
  - `"a ba"`
* - Форматированная строка (`using Printf`)
  - `@sprintf "fmt" x...`
  - `@sprintf "π ≈ %.2f, ℯ ≈ %.2f" π ℯ`
  - `"π ≈ 3.14, ℯ ≈ 2.72"`
```

## Функции

Основной синтаксис для функций в Julia

```julia-repl
julia> function f(x, y)
           return sqrt(x^2 + y^2)
       end
f (generic function with 1 method)
```

Строго говоря, слово `return` необязательно, функция в Julia возвращает результат последнего выражения, однако, мы будем придерживаться [![Code Style: Blue](https://img.shields.io/badge/code%20style-blue-4495d1.svg)](https://github.com/invenia/BlueStyle).

В теле функции может находится несколько слов `return`, в этом случае при вызове сработает только одно из них.

Если необходимо, чтобы функция ничего не возвращала, тогда используется `return nothing`.

Зачастую пригождается более краткий синтаксис (assignment form)

```julia-repl
julia> f(x, y) = sqrt(x^2 + y^2)
f (generic function with 1 method)
```

В этом случае справа от `=` может находится составное выражение (compound expression) `begin ... end`.

```julia-repl
julia> g(x, y) = begin              # или g(x, y) = begin z = f(x, y); return 2*z end
           z = f(x, y)
           return 2*z
       end
g (generic function with 1 method)
```

Синтаксис вызова интуитивен

```julia-repl
julia> f(3, 4)
5.0

julia> g(3, 4)
10.0
```

В Julia аргументы передаются по принципу *pass-by-sharing*. Т.е., аргументы функции внутри тела ведут себя как новые переменные. Однако, у изменяемых *mutable* аргументов (например, массив), можно поменять значения, и они будут видны извне. По соглашению, функции, меняющие свои аргументы, оканчиваются на `!`, например, `map!(f, A, B)`, `push!(A, a)`.

Функции являются **first-class** объектами, т.е. ими можно "распоряжаться", как переменными: присваивать их другим переменным, передавать в функции... 

```julia-repl
julia> φ = f;

julia> φ(3, 4)
5.0
```

Важно, что операторы в Julia также являются функциями

```julia-repl
julia> +(1, 2, 3) == 1 + 2 + 3
true
```

Языковые конструкции, вроде доступа к элементу массива или полю структуры также являются операторами, а следовательно, и функциями.

Можно создавать анонимные функции

```julia-repl
julia> x -> 2x                        # короткий синтаксис
#1 (generic function with 1 method)

julia> f4 = x -> 2x                   # анонимная функция x -> 2x присвоена переменной f4
#3 (generic function with 1 method)

julia> f4(8)
16

julia> function (x)                   # длинный синтаксис
           return 3x
       end
#5 (generic function with 1 method)

julia> map(x -> 3x, 1:4)              # пример применения
4-element Vector{Int64}:              # map(f, c) применяет функцию f
  3                                   # к каждому элементу коллекции c
  6
  9
 12
```

В Julia функция тесно связана с `Tuple`. Встроенный тип кортеж `Tuple` является неизменяемой коллекцией.

```julia-repl
julia> tup = (3, 4)
(3, 4)

julia> tup[1]
3

julia> (3,)  # tuple из одного значения
(3,)
```

Таким образом, функция может возращать `Tuple`, т.е. возвращать несколько значений. Вообще, функция в Julia тогда это "преобразование" одного кортежа значений в другой.

```julia-repl
julia> function addmul(x, y)
           return x + y, x * y
       end
addmul (generic function with 1 method)

julia> a, b = addmul(3, 4)
(7, 12)

julia> a
7

julia> b
12
```

Также существует `NamedTuple`, хранящий пары ключ-значение. На его основе создаются функции, принимающие аргументы по ключу.

Больше информации в разделе мануала [Functions](https://docs.julialang.org/en/v1/manual/functions/).

## Управляющие конструкции

Более подробно познакомиться можно здесь **[[url]](https://docs.julialang.org/en/v1/manual/control-flow/)**.

### Составные выражения

C двумя управляющими конструкциями мы уже встретились, это `begin`-блоки и `;`-цепочки.

Блок `begin ... end` позволяет создавать составное выражение *compound expression*. Иногда удобно несколько выражений объединить в одно.

```julia-repl
julia> z = begin
           x = 1
           y = 2
           x + y
       end
3
```

С помощью `;` можно поместить несколько выражений на одной строке, что часто используется для коротких составных выражений.

```julia-repl
julia> z = (x = 1; y = 2; x + y)
3

julia> z = begin x = 1; y = 2; x + y end
3
```

### Условное исполнение

Синтаксис `if-elseif-else` блока

```julia-repl
julia> x = 10; y = 20;

julia> if x < y
           println("x is less than y")
       elseif x > y
           println("x is greater than y")
       else
           println("x is equal to y")
       end
x is less than y
```

В `if` и `elseif` должно стоять boolean выражение. За это отвечает тип `Bool`, имеющий два значения: `true` и `false`. Основные операторы сравнения можете посмотреть здесь **[[url]](https://docs.julialang.org/en/v1/manual/mathematical-operations/#Numeric-Comparisons)**.

Количество `elseif` не ограничено.

Блок `if-elseif-else` подобно `begin-end` возвращает значение. В примере выше возвращается `nothing`, который вернулся от вызова `println("x is less than y")`. `Nothing` не печатается в языке.

Также в вашем распоряжении тернарный оператор

```julia
x = cond ? a : b
```

### Циклы

Julia предоставляет два цикла: `while` и `for`.

```julia-repl
julia> i = 1;

julia> while i <= 5
           println(i)
           global i += 1
       end
1
2
3
4
5
```

Наличие `global` связано с созданием цикла `while` собственного пространства переменных *variable scope*. В REPL это значения не имеет, а вот в скриптах вы получите ошибку, попробуйте запустить в REPL `include_string(Main, "i = 1; while i < 2; i += 1; end")`.

У цикла `for` синтаксис следующий

```julia
for var in iterator
    ...
end
```

Переменная цикла `var` создаётся внутри видимости цикла `for` автоматически. После исполнения цикла она недоступна, если не была объявлена во внешней области видимости.

```julia-repl
julia> for j in 1:3
           println(j)
       end
1
2
3

julia> j
ERROR: UndefVarError: j not defined
```

Здесь использован *range operator*

```julia-repl
help?> 1:3
  (:)(start, [step], stop)

  Range operator. a:b constructs a range from a to b with a step size of 1 (a UnitRange),
  and a:s:b is similar but uses a step size of s(a StepRange).
```

Итерировать можно и по коллекциям. Ниже пример для `Tuple`

```julia-repl
julia> for j in (1, 2, "hello")
           println(j)
       end
1
2
hello
```

(type_system)=
## Система типов
В Julia **сильная динамическая** система типов, являющейся центральной темой языка наряду с методами.

### Декларация типов

С помощью оператора `::` вы можете *декларировать* тип объекта. Обычно это делается для

- переменных;
- аргументов функции;
- возвращаемоего значения функцией;
- полей композитных типов (структур).

Например, вы можете декларировать тип переменной внутри функции (или другой локальной области видимости), в таком случае при присвоении значения происходит конвертация (приведение) `convert(T, x)` к типу.

```julia-repl
julia> function foo()
           x::Int8 = 100
           return x
       end
foo (generic function with 1 method)

julia> x = foo()
100

julia> typeof(x)
Int8

julia> typeof(100)
Int64
```

Также вы можете декларировать тип возвращаемого значения функцией (хотя, делается это редко)

```julia-repl
julia> bar()::Int8 = 100
bar (generic function with 1 method)

julia> bar()
100

julia> typeof(bar())
Int8
```

### Какие бывают типы

Типы в Julia классифицируется по следующим признакам (указаны не все)

- абстрактный *abstract* (`AbstractFloat`) и конкретный *concrete* (`Float64`);
- примитивный *primitive* (`Float64`) и композитный *composite* (`Complex{Float64}`);
- параметрический *parametric* (`Complex{Float64}`);
- изменяемый (`Vector{Float64}`) и неизменяемый (`Tuple{Float64,Float64}`);
- ...

В Julia система типов является деревом, корень которого тип `Any`. Листьями дерева типов являются **конкретные типы**. У значения такого типа известна структура в памяти. **Абстрактные** типы нужны в качестве промежуточных узлов дерева типов.

Например, так выглядит часть дерева с числовыми и строчными типами, при этом конкретные типы указаны в округлённых рамках, а абстрактные в прямоугольных

```{mermaid}
flowchart TB
    Any --> Number --> Real --> Integer
    Any --> AbstractString --> String([String])
            AbstractString --> SubString[/SubString/]
    Number --> Complex[/Complex/]
    Real --> AbstractFloat
    Real --> Rational[/Rational/]
    Rational --> RationalInt64(["Rational{Int64}"])
    Rational --> RationalInt32(["Rational{Int32}"])
    Integer --> Int64([Int64])
    Integer --> Int32([Int32])
    Integer --> Int16([Int16])
    Integer --> Int8([Int8])
```

Несколько функций для интроспекции системы типов

- `subtypes(T)`: подтипы типа `T`;
- `isabstracttype(T)`: является ли тип `T` абстрактным;
- `isprimitivetype(T)`: является ли тип `T` примитивным;
- `ismutable(x)`: является ли значение `x` изменяемым.

```{margin}
С точки зрения дерева, можно ли из `Ty` добраться до `Tx`.
```
Также с помощью subtype-оператора `Tx <: Ty` можно проверять "является ли тип `Tx` подтипом типа `Ty`".

```julia-repl
julia> Int64 <: Number
true
```

**Примитивные** типы представляются в виде набора бит. Такими примерами являются Int-ы и Float-ы.

```{note}
Создание собственных абстрактных и примитивных типов здесь не разбирается.
```

(julia_composite)=
### Композитные типы

**Композитный** тип имеет более сложную структуру в памяти. Этот тип является набором именнованных полей, а экземпляром такого типа можно распоряжаться как одним значением. В других языках им соответствуют объекты *objects* или структуры *structs*. Примерами встроенных композитых типов являются `Tuple`, `String`, `Array`, `IO`.

Классический пример - структура для точки в пространстве.

```julia-repl
julia> struct Point
           x
           y::Int64
       end

julia> p1 = Point(1.0, 2)
Point(1.0, 2)

julia> typeof(p1)
Point

julia> p1.x
1.0

julia> p1.x = 9
ERROR: setfield! immutable struct of type Point cannot be changed
...
```

Для поля `Point.y` указан тип.

Присвоение можно разрешить, если сделать структуру `mutable`

```julia-repl
julia> mutable struct MPoint
           x
           y
       end

julia> mp1 = MPoint(1, 2)
MPoint(1, 2)

julia> mp1.x = 10
10

julia> mp1
MPoint(10, 2)
```

Иногда это может приводить к замедлению работы, поскольку `mutable struct` выделяется в куче, а не на стеке.

Тем не менее, в поле обычной структуры можно хранить значение изменяемого типа. В таком случае в поле хранится ссылка на изменяемый объект. Саму же ссылку поменять нельзя.

Конструктор *constructor* `Point(x, y)` для композитного типа можно поменять. Более того, можно создать несколько конструкторов.

### Параметрические композитные типы

```{margin}
Абстрактные параметрические типы тоже существуют.
```
**Параметрический** тип это тип, который в своём объявлении содержит дополнительную информацию. Например, тип `Complex` в языке объявлен следующим образом **[[url]](https://github.com/JuliaLang/julia/blob/1326e4b00e6f27a4120c30eb12b578a6cee28039/base/complex.jl#L3)**

```julia
struct Complex{T<:Real} <: Number
    re::T
    im::T
end
```

Это объявление значит

```{margin}
`Complex` в данном случае не будет ни абстрактным, ни конкретным. В таких объявлениях создаются `UnionAll` типы.
```
- создать тип `Complex`, который будет вести себя как супертип для `Complex{T}`;
- тип `Complex{T}` является параметрическим типом,
- где тип `T` является подтипом типа `Real`,
- при этом сам тип `Complex{T}` является подтипом `Number`.

При этом указано, что поля типа `Complex` имеют одинаковый тип `T`.

```{margin}
Это не значит, что массив в Julia может хранить только значения одного типа. Ведь есть абстрактные типы: `Any`, `Number`...
```
В качестве параметров могут быть типы и значения примитивных типов. Например в Julia объявлен тип для массивов ` AbstractArray{T,N}`, где под `T` подразумевается тип значений, а под `N` размерность. 

Параметрические типы порождают целое семество типов. Член такого семейства - *любая  комбинация разрешенных значений* параметров типа.

Таким образом, с помощью параметризации композитного типа, мы можем дать подсказки для компилятора, чтобы тот создавал оптимизированный код.

#### Пример

Рассмотрим две версии `Point`

```julia-repl
julia> struct APoint
           x
           y
       end

julia> struct TPoint{T}
           x::T
           y::T
       end
```

Создадим функцию, вычисляющую расстояние от точки до начала координат

```julia-repl
julia> dist(p) = sqrt(p.x^2 + p.y^2)
dist (generic function with 1 method)
```

Заметьте, в этой функции нет никаких ограничений на тип `p`.

Создадим два массива случайных точек

```julia-repl
julia> AA = [APoint(rand(2)...) for _ in 1:1_000_000];

julia> TA = [TPoint(rand(2)...) for _ in 1:1_000_000];

julia> typeof(AA), typeof(TA)
(Vector{APoint}, Vector{TPoint{Float64}})
```

Про массив `TA` компилятор явно знает больше, судя по `typeof`.

Измерим время работы (в первые запуски вмешивается компиляция)

```julia-repl
julia> @time dist.(AA);
  0.138012 seconds (5.00 M allocations: 84.154 MiB, 6.08% gc time, 7.93% compilation time)

julia> @time dist.(AA);
  0.141266 seconds (5.00 M allocations: 83.916 MiB, 13.38% gc time)

julia> julia> @time dist.(TA);
  0.029862 seconds (8.17 k allocations: 8.015 MiB, 29.57% gc time, 63.13% compilation time)

julia> @time dist.(TA);
  0.001816 seconds (4 allocations: 7.630 MiB)
```

Серьёзный бенчмарк также показывает разницу в два порядка

```julia-repl
julia> using BenchmarkTools

julia> @btime dist.(AA);
  117.766 ms (4999502 allocations: 83.92 MiB)

julia> @btime dist.(TA);
  1.290 ms (5 allocations: 7.63 MiB)
```

## Методы и multiple dispatch


## Вектора и матрицы
% броадкаст
