{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": 1,
   "id": "707b5ec0",
   "metadata": {
    "tags": [
     "remove-cell"
    ]
   },
   "outputs": [
    {
     "data": {
      "text/plain": [
       "romberg"
      ]
     },
     "execution_count": 1,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "include(\"../src.jl\")"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "8f68cf73",
   "metadata": {},
   "source": [
    "# Метод Ромберга\n",
    "\n",
    "*Метод Ромберга* (*Romberg's method*) позволяет быстро интегрировать с заданной точностью. Метод основывается на применении экстраполяции Ричардсона к формуле трапеций при удвоении числа узлов.\n",
    "\n",
    "## Экстраполяция Ричардсона\n",
    "\n",
    "Частный случай этого метода мы рассматривали при выводе {ref}`Формулы Симпсона <sec:quadrature_simpson>`.\n",
    "\n",
    "{cite}`SamarskiyGulin1989` Рассмотрим вычисление интеграла $I_h$ на равномерной сетке с шагом $h$. На каждом интервале отрезка применяется одна и та же квадратурная формула. Для некоторых формул (например, для формулы трапеций) удаётся получить вид погрешности $I - I_h$. Предположим, что это разложение выглядит так\n",
    "\n",
    "```{math}\n",
    ":label: richardson_int\n",
    "\n",
    "I_h = I + a_1 h^{\\alpha_1} + a_2 h^{\\alpha_2} + ... + a_m h^{\\alpha_m} + O(h^{\\alpha_{m+1}}).\n",
    "```\n",
    "\n",
    "Здесь $0 < \\alpha_1 < \\alpha_2 < ... < \\alpha_m$ -- степени разложения, а $a_i$ его ненулевые коэффициенты.\n",
    "\n",
    "Теперь рассмотрим {eq}`richardson_int` на последовательности сеток с шагами $h_0 = h$, $h_1 = qh$, $h_2 = q^2 h$, ..., $h_k = q^k h$, $k=0,...,m$, $0 < q < 1$. Тогда, согласно {eq}`richardson_int`, запишем\n",
    "\n",
    "```{math}\n",
    ":label: richardson_decompose\n",
    "\n",
    "I_{h_k} = I + a_1 h_k^{\\alpha_1} + a_2 h_k^{\\alpha_2} + ... + a_m h_k^{\\alpha_m} + O(h_k^{\\alpha_{m+1}}),\\quad k=0,...,m.\n",
    "```\n",
    "\n",
    "Введём обозначение $I^{(1)}_{h_k} = I_{h_k}$ и запишем для двух сеток\n",
    "\n",
    "```{math}\n",
    "I^{(1)}_{h_{k-1}} = I + a_1 h_{k-1}^{\\alpha_1} + O(h^{\\alpha_2}_{k-1})\\\\\n",
    "I^{(1)}_{h_k} = I + a_1 h_k^{\\alpha_1} + O(h^{\\alpha_2}_{k}).\n",
    "```\n",
    "\n",
    "Скомбинируем выражения так, чтобы слагаемое при $a_1$ занулить, получим\n",
    "\n",
    "```{math}\n",
    ":label: richardson_step\n",
    "\n",
    "I = I^{(2)}_{h_{k-1}} + O(h^{\\alpha_2}_{k-1}),\n",
    "```\n",
    "\n",
    "где\n",
    "\n",
    "```{math}\n",
    "I^{(2)}_{h_{k-1}} = \\frac{I^{(1)}_{h_k} - q^{\\alpha_1}I^{(1)}_{h_{k-1}}}{1 - q^{\\alpha_1}}.\n",
    "```\n",
    "\n",
    "```{index} экстраполяция Ричардсона\n",
    "```\n",
    "Таким образом, мы выполнили один шаг **экстраполяции Ричардсона**. Важно отметить, что согласно {eq}`richardson_step` $I^{(2)}$ приближает $I$ лучше, чем $I^{(1)}$, у которых ошибка порядка $h^{\\alpha_1}$.\n",
    "\n",
    "Процесс экстраполяции можно продолжить, тогда в общем случае получим рекурретное выражение\n",
    "\n",
    "```{math}\n",
    ":label: richardson_general\n",
    "\n",
    "\\begin{align}\n",
    "I_{h_{k-1}}^{(j+1)} = \\frac{I^{(j)}_{h_k} - q^{\\alpha_j}I^{(j)}_{h_{k-1}}}{1 - q^{\\alpha_j}},\\\\\n",
    "j = 1,..., m, \\quad k = 1, 2, ..., m - j + 1, \\\\\n",
    "I_{h_k}^{(1)} = I_{h_k},\\quad k =0,1,...,m.\n",
    "\\end{align}\n",
    "```\n",
    "\n",
    "Точность приближения для {eq}`richardson_general` составляет\n",
    "\n",
    "```{math}\n",
    "I = I_{h_k}^{(j)} + O(h_k^{\\alpha_j}).\n",
    "```\n",
    "\n",
    "Важно отметить структуру формулы {eq}`richardson_general`: $(j+1)$-ое приближение на сетке с шагом $h_{k-1}$ можно получить по $(j)$-приближениям на сетках с шагами $h_k$ и $h_{k-1}$.\n",
    "\n",
    "```\n",
    "\\ j  1    2    3   \n",
    "k --------------\n",
    "0   01 → 02 → 03\n",
    "       ↗    ↗\n",
    "1   11 → 12\n",
    "       ↗\n",
    "2   21\n",
    "```\n",
    "\n",
    "```{index} метод; Ромберга\n",
    "```\n",
    "```{index} Ромберга метод\n",
    "```\n",
    "Теперь рассмотрим частный случай экстраполяции Ричардсона, **метод Ромберга**.\n",
    "\n",
    "Положим в основу экстраполяции формулу трапеций и будем мельчить сетку вдвое ($q=0.5$). Для составной формулы трапеций разложение {eq}`richardson_decompose` содержит только *чётные степени*, т.е. $\\alpha_j = 2 j$. Тогда, из {eq}`richardson_general` получим\n",
    "\n",
    "```{math}\n",
    ":label: romberg_method\n",
    "\n",
    "I_{h_{k-1}}^{(j+1)} = \\frac{4^j I^{(j)}_{h_k} - I^{(j)}_{h_{k-1}}}{4^j - 1}.\n",
    "```\n",
    "\n",
    "Прежде чем переходить к реализации, рассмотрим особенность формулы трапеции при измельчении сетки, это позволит избежать перевычислений в {eq}`romberg_method`.\n",
    "\n",
    "## Удвоение числа узлов\n",
    "\n",
    "Рассмотрим равномерную сетку $a = x_0 < x_1 < x_2 < x_3 < x_4 = b$ из $n=4$ интервалов с шагом $h_4$ и сетку из $n=2$ интервалов $a = x_0 < x_2 < x_4 =b$ с шагом $2h_4$.\n",
    "\n",
    "%%% todo: сюда бы картинку\n",
    "\n",
    "На крупной сетке формула трапеций даёт\n",
    "\n",
    "```{math}\n",
    ":label: doubling_tf2\n",
    "\n",
    "T_f(n=2) = 2h_4 \\Big[0.5 \\big(f(x_0) + f(x_4)\\big) + f(x_2)\\Big].\n",
    "```\n",
    "\n",
    "А на сетке вдвое мельче\n",
    "\n",
    "```{math}\n",
    ":label: doubling_tf4\n",
    "\n",
    "T_f(n=4) = h_4 \\Big[ 0.5 \\big(f(x_0) + f(x_4)\\big) + f(x_1) + f(x_3) \\Big].\n",
    "```\n",
    "\n",
    "Сравнивая {eq}`doubling_tf2` и {eq}`doubling_tf4`, обнаруживаем их связь\n",
    "\n",
    "```{math}\n",
    "T_f(n=4) = 0.5 T_f(n=2) + h_4 \\big(f(x_1) + f(x_3)\\big).\n",
    "```\n",
    "\n",
    "Таким образом, мы в частном случае получили, что вычислив интеграл на крупной сетке, мы можем вычислить его на сетке вдвое мельче, используя только значения в *новых узлах* ($x_1$, $x_3$).\n",
    "\n",
    "Это верно и в общем случае\n",
    "\n",
    "```{math}\n",
    ":label: trapezoid_2x\n",
    "\n",
    "T_f(2n) = 0.5 T_f(n) + h_{2n} \\sum_{i=1}^n f(x_{2i-1}), \\quad h_{2n} = \\frac{b-a}{2n},\\quad x_{2i-1} = a + h_{2n} (2i-1).\n",
    "```\n",
    "\n",
    "Данная формула позволяет вычислить интеграл с заданной точностью. Под точностью здесь понимается разница между новой аппроксимацией и старой\n",
    "\n",
    "```{math}\n",
    ":label: doubling_tol\n",
    "\n",
    "|T_f(2n) - T_f(n)| < \\text{tol}.\n",
    "```\n",
    "\n",
    "Например, при $\\text{tol} = 10^{-6}$ мы можем добиться точности до 6 знаков после запятой. Конечно, ошибки округления и обусловленность задачи никуда не делись.\n",
    "\n",
    "Также отметим, что выражение {eq}`doubling_tol` является абсолютной, а не относительной точностью.\n",
    "\n",
    "Реализация может быть такой\n",
    "\n",
    "(function:trapezoid_tol)=\n",
    ":::{proof:function} trapezoid_tol\n",
    "\n",
    "**Формула трапеций с контролем точности**\n",
    "\n",
    "```julia\n",
    "\"\"\"\n",
    "Вычисляет интеграл ∫`f`dx на [`a`, `b`] с точностью `atol` по формуле трапеций,\n",
    "удваивая число разбиений интервала, но не более `maxstep` раз.\n",
    "Возвращает значение интеграла.\n",
    "\"\"\"\n",
    "function trapezoid_tol(f, a, b; atol=1e-3, maxstep::Integer=100)\n",
    "    nc, hc = 1, b - a\n",
    "    Tc = hc * (f(a) + f(b)) / 2\n",
    "    for step in 1:maxstep\n",
    "        Tp, np = Tc, nc\n",
    "        hc /= 2\n",
    "        nc *= 2\n",
    "        Tc = Tp / 2 + hc * sum(f, (a + hc*(2i-1) for i in 1:np))\n",
    "        abs(Tc - Tp) < atol && return Tc\n",
    "    end\n",
    "    error(\"Точность не удовлетворена.\")\n",
    "end\n",
    "```\n",
    ":::\n",
    "\n",
    "## Реализация\n",
    "\n",
    "```{margin}\n",
    "Можно обойтись и двумя векторами.\n",
    "```\n",
    "Итак, хранить $I_{h_{k-1}}^{(j)}$ будем в элементе матрицы `I[k, j]`.\n",
    "\n",
    "Первый элемент `I[1, 1]` заполним вручную: он соответствует формуле трапеций на сетке из одного интервала (2 узла). Затем, пока не достигнем необходимой точности, будем делать следующее\n",
    "\n",
    "1. Измельчим сетку вдвое и подсчитаем интеграл по формуле трапеций на ней {eq}`trapezoid_2x`, так получим из `I[k, 1]` элемент `I[k+1, 1]`;\n",
    "2. Вычислим по доступной побочной диагонали слева направо все элементы от `I[k, 2]` вплоть до `I[1, k+1]` по формуле перехода {eq}`romberg_method`;\n",
    "3. Будем продолжать 1 и 2 до тех пор, пока приближения `I[1, k+1]` и `I[2, k]` станут неотличимы в заданной точности `atol`, или пока не исчерпаем количество измельчений `maxstep`.\n",
    "\n",
    "**Пример заполнения матрицы `I`**. Допустим, максимальное измельчение `maxstep = 4`, т.е. от $h=b-a$ до $h = (b-a) / 2^4$. Ниже приведена схема вычислений и обозначения\n",
    "\n",
    "- ⋅: элемент матрицы, который не пригодится в вычислениях;\n",
    "- □: элемент, который, возможно, предстоит определить;\n",
    "- ○ и ●: уже вычисленные элементы, причём ● сравниваются между собой в конце итерации;\n",
    "- стрелка вниз показывает переход по умельчению сетки (шаг 1);\n",
    "- остальные стрелки показывают переход по формуле (шаг 2).\n",
    "\n",
    "```{margin}\n",
    "Этот алгоритм является типичной задачей динамического программирования.\n",
    "```\n",
    "```\n",
    "     1 итерация             2 итерация           3 итерация             4 итерация\n",
    "\n",
    "○ → ●   □   □   □      ○   ○ → ●   □   □      ○   ○   ○ → ●   □      ○   ○   ○   ○ → ●\n",
    "↓ ↗                          ↗                          ↗                          ↗\n",
    "●   □   □   □   ⋅      ○ → ●   □   □   ⋅      ○   ○ → ●   □   ⋅      ○   ○   ○ → ●   ⋅\n",
    "                       ↓ ↗                          ↗                          ↗\n",
    "□   □   □   ⋅   ⋅      ○   □   □   ⋅   ⋅      ○ → ○   □   ⋅   ⋅      ○   ○ → ○   ⋅   ⋅\n",
    "                                              ↓ ↗                          ↗\n",
    "□   □   ⋅   ⋅   ⋅      □   □   ⋅   ⋅   ⋅      ○   □   ⋅   ⋅   ⋅      ○ → ○   ⋅   ⋅   ⋅\n",
    "                                                                     ↓ ↗\n",
    "□   ⋅   ⋅   ⋅   ⋅      □   ⋅   ⋅   ⋅   ⋅      □   ⋅   ⋅   ⋅   ⋅      ○   ⋅   ⋅   ⋅   ⋅\n",
    "```\n",
    "\n",
    "(function:romberg)=\n",
    ":::{proof:function} romberg\n",
    "\n",
    "**Метод Ромберга с контролем точности**\n",
    "\n",
    "```julia\n",
    "\"\"\"\n",
    "Вычисляет интеграл ∫`f`dx на отрезке [`a`, `b`] методом Ромберга.\n",
    "Разбивает отрезок пополам не более `maxstep` раз.\n",
    "Возвращает значение интеграла, если приближения отличаются не более чем на `atol`.\n",
    "\"\"\"\n",
    "function romberg(f, a, b; atol=1e-6, maxstep::Integer=100)\n",
    "    maxstep = max(2, maxstep)\n",
    "    I = Matrix{Float64}(undef, maxstep+1, maxstep+1)\n",
    "    I[1, 1] = (b - a) * (f(a) + f(b)) / 2\n",
    "    for i in 2:maxstep+1\n",
    "        let hc = (b - a) / 2^(i-1), np = 2^(i-2)\n",
    "            I[i, 1] = I[i-1, 1] / 2 + hc * sum(f, (a + hc * (2i-1) for i in 1:np))\n",
    "        end\n",
    "        for k in i-1:-1:1\n",
    "            I[k, i-k+1] = (2^i*I[k+1, i-k] - I[k, i-k]) / (2^i - 1)\n",
    "        end\n",
    "        abs(I[1, i] - I[2, i-1]) < atol && return I[1, i]\n",
    "    end\n",
    "    error(\"Точность не удовлетворена.\")\n",
    "end\n",
    "```\n",
    ":::\n",
    "\n",
    ":::{proof:demo}\n",
    ":::\n",
    "\n",
    "```{raw} html\n",
    "<div class=\"demo\">\n",
    "```\n",
    "\n",
    "Сравним время вычисления интеграла\n",
    "\n",
    "```{math}\n",
    "\\int_0^3 x e^{\\sin 2x}\n",
    "```\n",
    "\n",
    "нашими реализациями {ref}`trapezoid_tol <function:trapezoid_tol>` и {ref}`romberg <function:romberg>`.\n",
    "\n",
    "Вычисление по формуле трапеций с заданной точностью занимает"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 2,
   "id": "db03f1e2",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "  589.263 μs (29491 allocations: 524.59 KiB)\n"
     ]
    },
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "  8.480 μs (6 allocations: 79.89 KiB)\n"
     ]
    },
    {
     "data": {
      "text/plain": [
       "(4.115935482633098, 4.115980256204295)"
      ]
     },
     "execution_count": 2,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "foo(x) = x * exp(sin(2x))\n",
    "a, b = 0, 3\n",
    "atol = 1e-6\n",
    "Tint = @btime trapezoid_tol($foo, a, b; atol=$atol) seconds=1\n",
    "Rint = @btime romberg($foo, a, b; atol=$atol) seconds=1\n",
    "(Tint, Rint)"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "5b9728c8",
   "metadata": {},
   "source": [
    "```{raw} html\n",
    "</div>\n",
    "```"
   ]
  }
 ],
 "metadata": {
  "jupytext": {
   "formats": "md:myst",
   "text_representation": {
    "extension": ".md",
    "format_name": "myst"
   }
  },
  "kernelspec": {
   "display_name": "Julia",
   "language": "julia",
   "name": "julia-1.6"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.6.2"
  },
  "source_map": [
   13,
   17,
   278,
   285
  ]
 },
 "nbformat": 4,
 "nbformat_minor": 5
}